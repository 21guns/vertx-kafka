
buildscript {
  repositories {
    mavenLocal()
    maven {
      url 'http://creditcloud.com:8081/nexus/content/groups/public'
      credentials {
        username "jenkins"
        password "hudson"
      }
    }
  }
  dependencies {
    classpath 'se.transmode.gradle:gradle-docker:1.2'
  }
}


plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
  jcenter()
}

version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

apply plugin: 'docker'

def vertxVersion = '3.4.2'
def mainVerticleName = 'io.vertx.starter.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'

  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-web:$vertxVersion"
  compile("io.vertx:vertx-kafka-client:$vertxVersion") {
        exclude module:'log4j'
        exclude group:'org.slf4j'
  }
  compile "io.vertx:vertx-rx-java:$vertxVersion"
  compile 'ch.qos.logback:logback-classic:1.2.3'

  compile('org.hbase:asynchbase:1.8.0') {
    exclude group:'org.slf4j'
  }
  compile group: 'org.javassist', name: 'javassist', version: '3.21.0-GA'
  compile 'org.apache.commons:commons-lang3:3.6'

  testCompile "junit:junit:4.12"
  testCompile "io.vertx:vertx-unit:$vertxVersion"
}

shadowJar {
  classifier = 'fat'
  manifest {
      attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.0.2'
}

task docker(type: Docker, dependsOn: build) {
  applicationName = jar.baseName
  dockerfile = file('Dockerfile')
  doFirst {
    copy {
      from 'build/libs/vertx-kafka-1.0-SNAPSHOT-fat.jar'
      into "${stageDir}/target"
    }
  }
}
